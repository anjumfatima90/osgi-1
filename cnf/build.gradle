/*
 * Copyright (c) Contributors to the Eclipse Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0 
 */

/*
 * cnf Gradle build script
 */
/* After building, we need to refresh the bnd Projects to pickup the bnd plugins built in cnf. */
def compileJava = tasks.named('compileJava')

def processResources = tasks.named('processResources')

def refresh = tasks.register('refresh', WriteProperties.class) {
	onlyIf {
		compileJava.get().didWork || processResources.get().didWork
	}
	inputs.files(compileJava).withPropertyName(compileJava.name)
	inputs.files(processResources).withPropertyName(processResources.name)
	outputFile = layout.buildDirectory.file('refresh.properties')
	property('refreshed', System.currentTimeMillis())
	doFirst('refresh') { t ->
		logger.info 'Refresh Bnd workspace after building Bnd plugins in {}', project.name
		if (!bndWorkspace.refresh()) {
			parent.subprojects {
				if (plugins.hasPlugin('biz.aQute.bnd')) {
					bnd.project.propertiesChanged()
				}
			}
		}
		bndWorkspace.getPlugins() // reload workspace plugins
	}
}

/* Bnd plugins influence bundle contents, so add cnf output to jar inputs */
parent.subprojects {
	if (plugins.hasPlugin('biz.aQute.bnd')) {
		if (project != cnf) {
			tasks.named('jar') {
				inputs.files(refresh).withPropertyName(refresh.name)
			}
		}
	}
}
