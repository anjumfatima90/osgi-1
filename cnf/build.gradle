/*
 * Copyright (c) Contributors to the Eclipse Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0 
 */

/*
 * cnf Gradle build script
 */
/* After building, we need to refresh the bnd Projects to pickup the bnd plugins built in cnf. */
def refresh = tasks.register('refresh') {
  onlyIf {
    compileJava.didWork || processResources.didWork
  }
  doLast {
    logger.info 'Refresh bnd Projects after building bnd plugins in {}', project.name
    bnd.project.refresh()
    parent.subprojects {
      if (project != cnf) {
        bnd.project.refresh()
      }
    }
  }
}

tasks.named('classes') {
  finalizedBy refresh
}

/* bnd plugins influence bundle contents, so add cnf output to jar inputs */
parent.subprojects {
  if (project != cnf) {
    tasks.named('jar') {
      inputs.files cnf.sourceSets.main.output
    }
  }
}
