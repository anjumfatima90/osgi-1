/*
 * Copyright (c) Contributors to the Eclipse Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0 
 */

/*
 * osgi.ct Gradle test execution script
 */

tasks.addRule('Pattern: prepct.<name>: Prepare the CT for <name>.') { taskName ->
  if (taskName.startsWith('prepct.')) {
    tasks.create(taskName) {
      ext.book = taskName - 'prepct.'
      description "Prepare the CT for ${book}."
      group 'verification'
      ext.destinationDirectory = project.objects.directoryProperty().value(project.layout.buildDirectory.dir("osgi.ct.${book}"))
      inputs.files fileTree(riBuildDir).include('osgi.ri.core.jar', "osgi.ri.${book}.jar")
      inputs.files project.layout.buildDirectory.file("osgi.ct.${book}.jar")
      doFirst {
        project.delete(destinationDirectory)
        project.mkdir(destinationDirectory)
      }
      doLast {
        inputs.files.each { jar ->
          copy {
            from zipTree(jar)
            into destinationDirectory
          }
        }
      }
    }
  }
}

tasks.addRule('Pattern: ct.<name>: Run the CT for <name>.') { taskName ->
  if (taskName.startsWith('ct.')) {
    def book = taskName - 'ct.'
    def destinationDirectory = project.objects.directoryProperty().value(project.layout.buildDirectory.dir("osgi.ct.${book}"))
    def cts = zipTree(project.layout.buildDirectory.file("osgi.ct.${book}.jar")).matching {
      include '*.bnd'
    }
    def cttask = tasks.create(taskName) {
      description "Run the CT for ${book}."
      group 'verification'
    }
    cts.forEach { bndFile ->
      def bndName = bndFile.name - '.bnd'
      cttask.dependsOn tasks.create("${taskName}.${bndName}") {
        description "Run the ${bndName} CT for ${book}."
        group 'verification'
        dependsOn tasks.named("prepct.${book}")
        doLast {
          javaexec {
            workingDir file(destinationDirectory)
            main = '-jar' // first arg must be the bnd jar
            args file(destinationDirectory.file('jar/bnd.jar'))
            args '--exceptions'
            args 'runtests'
            args '--title', name
            args '--reportdir', "reports/${bndName}"
            args bndFile.name
          }.assertNormalExitValue()
        }
      }
    }
  }
}
